#version 120

uniform sampler2D lastBuffer;

const vec2 rcpFrame = vec2(1.0/853.0, 1.0/480.0);

vec3 fxaa(sampler2D tex, vec2 uv)
{
	vec3 rgbNW = texture2D(tex, uv + vec2(-0.5, -0.5)*rcpFrame).rgb;
	vec3 rgbNE = texture2D(tex, uv + vec2( 0.5, -0.5)*rcpFrame).rgb;
	vec3 rgbSW = texture2D(tex, uv + vec2(-0.5,  0.5)*rcpFrame).rgb;
	vec3 rgbSE = texture2D(tex, uv + vec2( 0.5,  0.5)*rcpFrame).rgb;
	vec3 rgbM = texture2D(tex, uv).rgb;

	const vec3 luma = vec3(0.299, 0.587, 0.114);
	float lumaNW = dot(rgbNW, luma);
	float lumaNE = dot(rgbNE, luma);
	float lumaSW = dot(rgbSW, luma);
	float lumaSE = dot(rgbSE, luma);
	float lumaM = dot(rgbM, luma);

	float lumaMin = min(lumaM, min(min(lumaNW, lumaNE), min(lumaSW, lumaSE)));
	float lumaMax = max(lumaM, max(max(lumaNW, lumaNE), max(lumaSW, lumaSE)));

	vec2 dir;
	dir.x = -((lumaNW + lumaNE) - (lumaSW + lumaSE));
	dir.y =  ((lumaNW + lumaSW) - (lumaNE + lumaSE));

	const float reduceMin = 1.0/128.0;
	const float reduceMul = 1.0/8.0;
	const float spanMax = 8.0;
	float dirReduce = max(
		(lumaNW + lumaNE + lumaSW + lumaSE) * (0.25 * reduceMul),
		reduceMin);
	float rcpDirMin = 1.0/(min(abs(dir.x), abs(dir.y)) + dirReduce);
	dir = clamp(dir * rcpDirMin, vec2(-spanMax), vec2(spanMax)) * rcpFrame;

	vec3 rgbA = 0.5 * (
		texture2D(tex, uv + dir * (1.0/3.0 - 0.5)).rgb +
		texture2D(tex, uv + dir * (2.0/3.0 - 0.5)).rgb);
	vec3 rgbB = rgbA * 0.5 + 0.25 * (
		texture2D(tex, uv + dir * (0.0/3.0 - 0.5)).rgb +
		texture2D(tex, uv + dir * (3.0/3.0 - 0.5)).rgb);

	float lumaB = dot(rgbB, luma);
	return (lumaB < lumaMin || lumaB > lumaMax) ? rgbA : rgbB;
}

void main()
{
	vec2 uv = gl_TexCoord[0].xy;
	gl_FragColor = vec4(fxaa(lastBuffer, uv), texture2D(lastBuffer, uv).a);
}
